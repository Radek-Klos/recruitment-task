plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

ext {
    androidxLifecycleVersion = "2.4.0"
    koinVersion= "3.1.5"
    okhttp3Version = "4.3.1"
    retrofitVersion = "2.6.1"
    glideVersion = "4.12.0"
    tatarkaVersion = "4.0.0"
    timberVersion = "4.7.1"
}

android {
    compileSdk 31

    defaultConfig {
        applicationId "com.peye.characters"
        minSdk 23
        targetSdk 31
        versionCode 1
        versionName "1.0"

        // Thanks to defining the base url here it could be defined per build flavour
        buildConfigField "String", "CHARACTER_API_BASE_URL", "\"https://rickandmortyapi.com/api/\""

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    dataBinding {
        enabled = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            buildConfigField "boolean", "ENABLE_HTTP_LOGGING", "true"
            versionNameSuffix ".debug"
        }
        release {
            minifyEnabled true
            buildConfigField "boolean", "ENABLE_HTTP_LOGGING", "false"
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            // adequate signingConfig goes here...
        }
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$androidxLifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$androidxLifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$androidxLifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:2.4.0"

    // Android Nav Component
    implementation "androidx.navigation:navigation-fragment-ktx:$androidxNavigationVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$androidxNavigationVersion"

    // DI Koin components
    implementation "io.insert-koin:koin-core:$koinVersion"
    implementation "io.insert-koin:koin-android:$koinVersion"

    // Networking
    implementation "com.squareup.okhttp3:okhttp:$okhttp3Version"
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'

    // Image Library
    implementation "com.github.bumptech.glide:glide:$glideVersion"

    // Collection-binding support for the Android Data Binding framework
    implementation "me.tatarka.bindingcollectionadapter2:bindingcollectionadapter-recyclerview:$tatarkaVersion"

    // Logging
    implementation "com.jakewharton.timber:timber:$timberVersion"
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.2'

    // Testing
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}
